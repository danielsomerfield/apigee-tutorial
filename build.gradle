import org.apache.http.auth.AuthScope
import org.apache.http.auth.UsernamePasswordCredentials
import org.apache.http.client.CredentialsProvider
import org.apache.http.client.methods.HttpPost
import org.apache.http.impl.client.BasicCredentialsProvider
import org.apache.http.impl.client.HttpClients

import static BuildUtils.ping
import static BuildUtils.generateVersion
import static BuildUtils.waitUntil
import static BuildUtils.waitForPing
import static BuildUtils.startApp

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.apache.httpcomponents:httpclient:4.3.6'
    }
}

group = "com.danielsomerfield"
mainClassName = "helloService.HelloServiceMain"
sourceCompatibility = 1.8
version = generateVersion()

def distroPath = "build/distributions/${project.name}-${project.version}.zip"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.+'
    compile 'org.eclipse.jetty:jetty-http:8.1.+'
    compile 'org.eclipse.jetty:jetty-server:8.1.+'
    testCompile 'org.apache.httpcomponents:httpclient:4.3.+'
    testCompile 'junit:junit:4.+'
}

test {
    exclude '**/*UATest.*'
}

task uat(type: Test, dependsOn: ['test', 'extractDistro']) {
    include '**/*UATest.*'
    doFirst {
        logger.info("Starting the app")
        project.ext.process = startApp(project.name, project.version)
        waitForPing()
    }
}
uat.finalizedBy("cleanup")

task cleanup() {
    doFirst {
        if (project.ext.process != null) {
            logger.info("Killing the app")
            project.ext.process.destroy()
        }
    }
}

task smokeTest(type: Test) {
    doFirst {
        waitForPing(60 * 1000)
    }
}


task pingApp() {
    doFirst {
        def url = "${System.getenv("HELLO_SERVICE_ROOT") ?: "http://localhost:8080"}/ping/"
        logger.info("Starting app ping to url $url")
        waitUntil(1000 * 60) {
            ping(url)
        }
    }
}


task extractDistro(type: Copy, dependsOn: 'distZip') {
    from(zipTree(distroPath))
    into 'build/exploded'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(distroPath)
        }
    }

    repositories {
        maven {
            url "https://api.bintray.com/maven/danielsomerfield/maven/apigee-tutorial"
            credentials {
                username = System.getenv("BINTRAY_USERNAME")
                password = System.getenv("BINTRAY_PASSWORD")
            }
        }
    }
}

publish {
    doLast {
        def bintrayPublishURL = "https://api.bintray.com/content/danielsomerfield/maven/apigee-tutorial/${version}/publish"

        HttpPost post = new HttpPost(bintrayPublishURL)
        CredentialsProvider credsProvider = new BasicCredentialsProvider();
        credsProvider.setCredentials(
                new AuthScope(null, -1),
                new UsernamePasswordCredentials(
                        System.getenv("BINTRAY_USERNAME"),
                        System.getenv("BINTRAY_PASSWORD")
                )
        );

        HttpClients.custom().setDefaultCredentialsProvider(credsProvider).build().withCloseable { client ->
            client.execute(post).withCloseable() { response ->
                if (response.statusLine.statusCode != 200) {
                    throw new GradleException("Bintray publish receive status code ${response.statusLine.statusCode}.")
                }
            }
        }
    }
}
