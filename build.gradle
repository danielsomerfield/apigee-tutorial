import static BuildUtils.generateVersion
import static BuildUtils.waitForPing
import static BuildUtils.startApp
import static PublishUtils.bintrayPublish

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.apache.httpcomponents:httpclient:4.3.6'
    }
}

allprojects {
    group = "com.danielsomerfield"
    version = generateVersion()

    repositories {
        mavenCentral()
    }
}

mainClassName = "helloService.HelloServiceMain"
sourceCompatibility = 1.8

def distroPath = "build/distributions/${project.name}-${project.version}.zip"


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.+'
    compile 'org.eclipse.jetty:jetty-http:8.1.+'
    compile 'org.eclipse.jetty:jetty-server:8.1.+'
    testCompile 'org.apache.httpcomponents:httpclient:4.3.+'
    testCompile 'junit:junit:4.+'
}

test {
    exclude '**/*UATest.*'
    exclude '**/*SmokeTest.*'
    exclude '**/*SecurityTest.*'
}

task uat(type: Test, dependsOn: ['test', 'extractDistro']) {
    include '**/*UATest.*'
    doFirst {
        logger.info("Starting the app")
        project.ext.process = startApp(project.name, project.version)
        waitForPing(5000)
    }
}
uat.finalizedBy("cleanup")

task cleanup() {
    doFirst {
        if (project.ext.process != null) {
            logger.info("Killing the app")
            project.ext.process.destroy()
        }
    }
}

task smokeTest(type: Test) {
    include '**/*SmokeTest.*'
    doFirst {
        waitForPing(60 * 1000)
    }
}

task securityTest(type:Test) {
    include '**/*SecurityTest.*'
    doFirst {
        waitForPing(60 * 1000)
    }
}

task extractDistro(type: Copy, dependsOn: 'distZip') {
    from(zipTree(distroPath))
    into 'build/exploded'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(distroPath)
        }
    }

    repositories {
        maven {
            url "https://api.bintray.com/maven/danielsomerfield/maven/apigee-tutorial"
            credentials {
                username = System.getenv("BINTRAY_USERNAME")
                password = System.getenv("BINTRAY_PASSWORD")
            }
        }
    }
}

publish {
    doLast {
        bintrayPublish(version)
    }
}
